/* -*- tab-width: 4; -*- */
/*
 * Copyright Â© 2018, Intel Corporation.
 *
 * This program is free software; you can redistribute it and/or modify it
 * under the terms and conditions of the GNU Lesser General Public License,
 * version 2.1, as published by the Free Software Foundation.
 *
 * This program is distributed in the hope it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 * FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
 * more details.
 *
 */

module port_cons_types {

type enumerated port_cons_op_t {
	PORT_CONS_OP_LISTEN,
	PORT_CONS_OP_CONNECT
}

type port port_cons message {
	inout charstring;
	inout port_cons_op_t;
}

/**
 * Smoke test
 */
type component cons_ct {
	port port_cons C; /* client */
	port port_cons S; /* server */
	timer T := 1.0;
}

testcase tc_port_cons() runs on cons_ct {

	var port_cons_op_t op;
	var boolean toggle := false;
	var integer count := 0;

	map(self:S, system:S);
	op := PORT_CONS_OP_LISTEN;
	S.send(op);

	map(self:C, system:C);
	op := PORT_CONS_OP_CONNECT;
	C.send(op);

	T.start;

	alt {
	[] S.receive("abc\n") {

		T.stop;

		count := count + 1;

		if (count > 1) {
			setverdict(pass); stop;
		}

		T.start; repeat;
	}
	[] S.receive(charstring:?) {

		setverdict(fail); stop;

	}
	[] T.timeout {

		if (toggle == false) {
			C.send("abc");
			toggle := true;
		} else {
			C.send("\n");
			toggle := false;
		}

		T.start; repeat;
	}
	}
}

control {
	execute(tc_port_cons());
}

}
