/* -*- c-basic-offset: 4; tab-width: 4; -*- */
/*
 * Copyright Â© 2018-2019, Intel Corporation.
 *
 * This program is free software; you can redistribute it and/or modify it
 * under the terms and conditions of the GNU Lesser General Public License,
 * version 2.1, as published by the Free Software Foundation.
 *
 * This program is distributed in the hope it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 * FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
 * more details.
 *
 */

/*
 * This is a sanity check for experimental TCP for Zephyr OS:
 * https://github.com/ozhuraki/zephyr/blob/tcp2-dev/README.md
 */

module tcp2_check {

import from libtypes all;
import from libnetinet all;
import from libtest all;
import from libutils all;
import from port_data_types all;

modulepar {
	float T_default := 0.1; /* default message timeout, sec */
	float TT_default := 2.0; /* default test teardonw timeout, sec */
	boolean fuzzer_enabled := false;
	boolean peer_assert_enabled := true;
	integer number_of_runs := 1;
	integer max_tests_failed := 1;
}

type bitstring event_t length(3);

const event_t EV_NONE			:='000'B;
const event_t EV_KEEP			:='111'B;
const event_t EV_TIMEOUT		:='001'B;
const event_t EV_UNEXPECTED_MSG :='010'B;

type bitstring log_mask_t length(3);

type record of octetstring tcp_test_data_t;

/* Fuzzer component for incoming/outgoing drop, delay, reorder, corrupt */
/* Can also be used for transparent trace */
type component fuzzer_ct {
	port port_tcp P_TCP_IN;		/* from/to suite */
	port port_tcp P_TCP_OUT;	/* from/to system under test */
	port port_udp P_UDP_IN;		/* from/to suite */
	port port_udp P_UDP_OUT;	/* from/to system under test */
	port port_cf P_CF;			/* configuration port */
}

type component tcp_ct extends test_CT {
	var tcp_t tcp;
	var tcp_state_t s := TCP_NONE;
	var uint32_t seq := 0;
	var uint32_t ack := 0;
	var octetstring data_unack := ''O; /* TODO: split into rcv/snd data */
	var integer data_step := 1;
	timer T := T_default;
	timer TT := TT_default; /* TODO: move into libtest */
	var event_t events := EV_NONE;
	var default d_as_timeout;
	var default d_as_unexpected_msg;
	var fuzzer_ct ct_fuzzer;
	port port_cf P_FZ;
}

function fuzzer() runs on fuzzer_ct
{
	var charstring cmd;
	var tcp_t tcp;
	var udp_t udp;
	var boolean recv := true;

	while (true) {
		alt {
		[] P_CF.receive("incoming_pass") { recv := true; }
		[] P_CF.receive("incoming_drop") { recv := false; }
		[] P_TCP_IN.receive(?) -> value tcp { /* data from suite */
			log("sut <- suite");
			P_TCP_OUT.send(tcp);
		}
		[] P_TCP_OUT.receive(?) -> value tcp { /* data from peer */
			if (recv) {
				log("sut -> suite");
				P_TCP_IN.send(tcp);
			}
		}
		[] P_UDP_IN.receive(?) -> value udp { /* data from suite */
			P_UDP_OUT.send(udp);
		}
		[] P_UDP_OUT.receive(?) -> value udp { /* data from peer */
			P_UDP_IN.send(udp);
		}
		} /* end of alt */
	}
}

function tcp_dump() runs on tcp_ct
{
	log("state=", tcp_state_to_string(s), " seq=", seq, " ack=", ack);
}

altstep as_timeout() runs on tcp_ct
{
	[] T.timeout { log("as_timeout()"); setverdict(fail); stop; }
}

altstep as_unexpected_msg() runs on tcp_ct
{
	[] P_TCP.receive(?) {
		log("as_unexpected_msg()");
		tcp_dump();
		setverdict(fail);
		stop;
	}
}

#define EVENT_IS(_event, _kind) mask_is_present(_event, _kind)
#define EVENT_IS_SET(_event, _kind) mask_is_present(_event, _kind)
#define EVENT_SET(_event, _kind) mask_set(_event, _kind)
#define EVENT_CLEAR(_event, _kind) mask_clear(_event, _kind)

function fail_on_events(event_t event) runs on tcp_ct
{
	if (event == EV_KEEP) {
		return;
	}

	if (EVENT_IS(event, EV_TIMEOUT) and not EVENT_IS_SET(events, EV_TIMEOUT)) {
		d_as_timeout := activate(as_timeout());
		EVENT_SET(events, EV_TIMEOUT);
	}

	if (not EVENT_IS_SET(event, EV_TIMEOUT) and
			EVENT_IS_SET(events, EV_TIMEOUT)) {
		deactivate(d_as_timeout);
		EVENT_CLEAR(events, EV_TIMEOUT);
	}

	if (EVENT_IS(event, EV_UNEXPECTED_MSG) and not
			EVENT_IS_SET(events, EV_UNEXPECTED_MSG)) {
		d_as_unexpected_msg := activate(as_unexpected_msg());
		EVENT_SET(events, EV_UNEXPECTED_MSG);
	}

	if (not EVENT_IS_SET(event, EV_UNEXPECTED_MSG) and
			EVENT_IS_SET(events, EV_UNEXPECTED_MSG)) {
		deactivate(d_as_unexpected_msg);
		EVENT_CLEAR(events, EV_UNEXPECTED_MSG);
	}
}

function st(tcp_state_t new, event_t fail_on := EV_KEEP) runs on tcp_ct
{
	/* TODO: Add debug levels */
	//log(tcp_state_to_string(s), " -> ", tcp_state_to_string(new));
	s := new;
	fail_on_events(fail_on);
}

type enumerated tp_msg_t {
	TP_COMMAND,
	TP_CONFIG_REQUEST,
	TP_CONFIG_REPLY,
	TP_INTROSPECT_REQUEST,
	TP_INTROSPECT_REPLY,
	TP_INTROSPECT_MEMORY_REQUEST,
	TP_INTROSPECT_MEMORY_REPLY,
	TP_INTROSPECT_PACKETS_REQUEST,
	TP_INTROSPECT_PACKETS_RESPONSE,
	TP_DEBUG_STOP,
	TP_DEBUG_STEP,
	TP_DEBUG_CONTINUE,
	TP_DEBUG_RESPONSE,
	TP_DEBUG_BREAKPOINT_ADD,
	TP_DEBUG_BREAKPOINT_DELETE,
	TP_TRACE_ADD,
	TP_TRACE_DELETE
} with {
	encode "TEXT";
}

external function tp_msg_to_string(in tp_msg_t x) return charstring
	with { extension "prototype(convert) encode(TEXT)"; }

type record tp_t {
	charstring msg,
	charstring status optional,
	charstring state optional,
	integer seq optional,
	integer ack optional,
	charstring rcv optional,
	charstring data optional,
	charstring op optional
} with {
	encode "JSON";
}

template tp_t ts_TP(tp_msg_t t,
					template charstring d := omit,
					template charstring o := omit) := {
	msg := tp_msg_to_string(t),
	status := omit,
	state := omit,
	seq := omit,
	ack := omit,
	rcv := omit,
	data := d,
	op := o
}

external function f_tp_enc(in tp_t x) return octetstring
	with { extension "prototype(convert) encode(JSON)"; }

external function f_tp_dec(in octetstring x) return tp_t
	with { extension "prototype(convert) decode(JSON)"; }

function tp_send(tp_msg_t msg_type,
				charstring o := "",
				charstring d := "") runs on tcp_ct return tp_t
{
	var tp_t tp := valueof(ts_TP(msg_type, d, o));
	var udp_t udp;

	if (msg_type == TP_COMMAND and o == "CLOSE") {
		tcp_config({{ "tp_trace", "0" }, { "tp_tcp_conn_delete", "1" }});
	}

	P_UDP.send(ts_UDP(d := f_tp_enc(tp)));

	/* Currently only introspect and certain command messages expect response */
	/* TODO: rework and get rid of the following if ()*/
	if (not (msg_type == TP_INTROSPECT_REQUEST or
			 (msg_type == TP_COMMAND and o == "RECV"))) {
		tp := valueof(ts_TP(msg_type));
		goto exit;
	}

	T.start;

	alt {
	[] P_UDP.receive(?) -> value udp {
			T.stop;
			tp := f_tp_dec(udp.data);
	}
	[] T.timeout {
		log("tp_send() timeout");
		setverdict(fail);
	}
	} /* end of alt */
label exit;
	return tp;
}

function peer_assert(tcp_state_t state) runs on tcp_ct
{
	if (not peer_assert_enabled) { return; }

	var tp_t tp := tp_send(TP_INTROSPECT_REQUEST);

	if (tp.state != tcp_state_to_string(state)) {
		log("ASSERT: peer: ", tp.state, " != ", tcp_state_to_string(state));
		setverdict(fail);
	}
}

function fuzzer_on(octetstring data_to_send := ''O) runs on tcp_ct
{
	ct_fuzzer := fuzzer_ct.create;

	disconnect(self:P_TCP, eth_ct:P_TCP);

	connect(self:P_TCP, ct_fuzzer:P_TCP_IN);
	connect(ct_fuzzer:P_TCP_OUT, eth_ct:P_TCP);

	disconnect(self:P_UDP, eth_ct:P_UDP);

	connect(self:P_UDP, ct_fuzzer:P_UDP_IN);
	connect(ct_fuzzer:P_UDP_OUT, eth_ct:P_UDP);

	connect(self:P_FZ, ct_fuzzer:P_CF);

	ct_fuzzer.start(fuzzer());
}

/**
 * Peer's active connection establishment, data transfer
 * (data echoing expected) and suite's active close
 */
/* TODO: merge test_tcp_connect() and test_tcp_listen()? */
function test_tcp_connect(octetstring data_to_send := ''O,
						  octetstring data_to_receive := ''O,
								boolean close := true,
								integer isn := 0) runs on tcp_ct
{
	var octetstring data := ''O;
	var octetstring data_received := ''O;

	seq := isn;

	tcp_init();

	if (fuzzer_enabled) { fuzzer_on(); }

	data_push(data, data_to_send);

	TT.start;

	P_TCP.send(ts_TCP(f := TH_SYN, th_seq := post_inc(seq)));
	st(TCP_SYN_SENT, fail_on := EV_TIMEOUT or4b EV_UNEXPECTED_MSG);
	T.start;

	alt {
	[s == TCP_SYN_SENT] P_TCP.receive(tr_TCP(f := TH_SYN or4b TH_ACK,
											 th_ack := seq)) -> value tcp {
		T.stop;
		peer_assert(TCP_SYN_SENT);
		st(TCP_SYN_RECEIVED);
		ack := tcp.th_seq;
		P_TCP.send(ts_TCP(f := TH_ACK, th_seq := seq, th_ack := pre_inc(ack)));
		st(TCP_ESTABLISHED, fail_on := EV_NONE);
		if (close == false) {
			goto end;
		}
		T.start; repeat;
	}

	[s == TCP_ESTABLISHED] P_TCP.receive(tr_TCP) -> value tcp {
		T.stop;
		peer_assert(TCP_ESTABLISHED);
		var tcp_t tcp2 := valueof(ts_TCP);
		if (th_is_set(tcp, TH_PSH)) {
			data_push(data_received, tcp.data);
			th_set(tcp2, TH_ACK);
			pre_inc(ack, lengthof(tcp.data));
		}
		if (th_is_set(tcp, TH_ACK)) {
			if (tcp.th_ack > seq - lengthof(data_unack)) {
				var integer diff := tcp.th_ack - (seq - lengthof(data_unack));
				data_pop(data_unack, diff);
			}
		}
		if (tcp2.th_flags != TH_NONE) {
			tcp2.th_seq := seq;
			tcp2.th_ack := ack;
			P_TCP.send(tcp2);
		}
		T.start; repeat;
	}
	[s == TCP_ESTABLISHED] T.timeout {
		peer_assert(TCP_ESTABLISHED);
		if (lengthof(data_unack) > 0) { goto wait_for_data; }
		if (lengthof(data_to_send) > 0) {
			data_unack := data_pop(data_to_send, data_step);
			P_TCP.send(ts_TCP(f := TH_PSH,
							  th_seq := post_inc(seq, lengthof(data_unack)),
							  d := data_unack));
			goto wait_for_data;
		}

		if (close == false) {
			goto end;
		}

		/* Everything is sent and aknowledged, do active close */
		P_TCP.send(ts_TCP(f := TH_FIN or4b TH_ACK, th_seq := post_inc(seq),
						  th_ack := ack));
		st(TCP_FIN_WAIT1, fail_on := EV_TIMEOUT or4b EV_UNEXPECTED_MSG);

		/* TODO: peer_assert() here is tricky, since peer in active close
		   sends ack and fin+ack immedeately */
label wait_for_data;
		T.start; repeat;
	}
	[s == TCP_FIN_WAIT1] P_TCP.receive(tr_TCP(f := TH_ACK,
											  th_seq := ack, th_ack := seq)) {
		T.stop;
		/* TODO: peer_assert() here is tricky, since peer in active close
		   sends ack and fin+ack immedeately */
		st(TCP_FIN_WAIT2);
		T.start; repeat;
	}
	[s == TCP_FIN_WAIT2] P_TCP.receive(tr_TCP(f := TH_FIN or4b TH_ACK,
											  th_seq := ack, th_ack := seq)) {
		T.stop;
		peer_assert(TCP_LAST_ACK);
		P_TCP.send(ts_TCP(f := TH_ACK, th_seq := seq, th_ack := pre_inc(ack)));
		st(TCP_TIME_WAIT, fail_on := EV_NONE);
		T.start; repeat; /* T * 2 */
	}
	[s == TCP_TIME_WAIT] T.timeout {
		st(TCP_CLOSED);
		peer_assert(TCP_CLOSED);
	}
	[] TT.timeout { setverdict(fail); stop; }
	}  /* end of alt */

	if (s == TCP_CLOSED and
			lengthof(data_to_send) == 0 and
			lengthof(data_unack) == 0 and
			data_received == data_to_receive) {
		setverdict(pass);
	} else {
		log("Error: data_to_receive=", data_to_receive, " ",
			"data_received=", data_received);
		setverdict(fail);
	}

	tp_send(TP_COMMAND, "CLOSE");
label end;
}

/**
 * Test peer's passive connection establishment, data transfer and close
 */
function test_tcp_listen(octetstring data_to_receive := ''O,
								boolean close := true,
								integer isn := 0) runs on tcp_ct
{
	var octetstring data := ''O; /* data received from the peer */

	tcp_init();

	TT.start;

	tp_send(TP_COMMAND, "CONNECT", d := oct2str(data_to_receive));

	st(TCP_LISTEN, fail_on := EV_TIMEOUT or4b EV_UNEXPECTED_MSG);
	T.start;

	alt {
	[s == TCP_LISTEN] P_TCP.receive(tr_TCP(f := TH_SYN)) -> value tcp {
		T.stop;
		peer_assert(TCP_SYN_SENT);
		st(TCP_SYN_RECEIVED);
		ack := tcp.th_seq;
		P_TCP.send(ts_TCP(f := TH_SYN or4b TH_ACK, th_seq := post_inc(seq),
						  th_ack := pre_inc(ack)));
		st(TCP_SYN_SENT);
		T.start; repeat;
	}
	[s == TCP_SYN_SENT] P_TCP.receive(tr_TCP(f := TH_ACK,
											 th_ack := seq)) -> value tcp {
		T.stop;
		st(TCP_SYN_RECEIVED);
		ack := tcp.th_seq;
		st(TCP_ESTABLISHED, fail_on := EV_NONE);
		if (close == false) {
			goto end;
		}
		T.start; repeat;
	}
	[s == TCP_ESTABLISHED] P_TCP.receive(tr_TCP(f := TH_PSH, th_seq := ack,
												d := ?)) -> value tcp {
		T.stop;
		peer_assert(TCP_ESTABLISHED);
		P_TCP.send(ts_TCP(f := TH_ACK, th_seq := seq,
						  th_ack := pre_inc(ack, lengthof(tcp.data))));
		data_push(data, tcp.data);
		T.start; repeat;
	}
	[s == TCP_ESTABLISHED] T.timeout {
		peer_assert(TCP_ESTABLISHED);
		if (data == data_to_receive) {
			/* Everything is received, do active close */
			P_TCP.send(ts_TCP(f := TH_FIN or4b TH_ACK, th_seq := post_inc(seq),
							  th_ack := ack));
			st(TCP_FIN_WAIT1, fail_on := EV_TIMEOUT or4b EV_UNEXPECTED_MSG);
		}
		T.start; repeat;
	}
	[s == TCP_FIN_WAIT1] P_TCP.receive(tr_TCP(f := TH_ACK,
											  th_seq := ack, th_ack := seq)) {
		T.stop;
		st(TCP_FIN_WAIT2);
		T.start; repeat;
	}
	[s == TCP_FIN_WAIT2] P_TCP.receive(tr_TCP(f := TH_FIN or4b TH_ACK,
											  th_seq := ack, th_ack := seq)) {
		T.stop;
		peer_assert(TCP_LAST_ACK);
		P_TCP.send(ts_TCP(f := TH_ACK, th_seq := seq, th_ack := pre_inc(ack)));
		st(TCP_TIME_WAIT, fail_on := EV_NONE);
		T.start; repeat; /* TODO: T * 2 */
	}
	[s == TCP_TIME_WAIT] T.timeout {
		st(TCP_CLOSED);
		peer_assert(TCP_CLOSED);
	}
	[] TT.timeout { setverdict(fail); stop; }
	} /* end of alt */

	if (s == TCP_CLOSED and data_to_receive == data) {
		setverdict(pass);
	} else {
		log("Error: data_to_receive=", data_to_receive, " ",
			"data=", data);
		setverdict(fail);
	}

	tp_send(TP_COMMAND, "CLOSE");
label end;
}

/**
 * Test active TCP connection establishment and active close
 */
testcase test_tcp_connect_no_data_close() runs on tcp_ct
{
	test_tcp_connect(data_to_send := ''O);
}

/**
 * Test peer's active TCP connection establishment,
 * data transfer (data echoing expected) and suite's active close
 */
testcase test_tcp_connect_data_close() runs on tcp_ct
{
	test_tcp_connect(data_to_send := '01'O, data_to_receive := '01'O);
}

/**
 * Test active TCP connection establishment,
 * "more data" data transfer (data echoing expected) and active close
 */
testcase test_tcp_connect_more_data_close() runs on tcp_ct
{
	test_tcp_connect(data_to_send := '010203'O, data_to_receive := '010203'O);
}

/**
 * Test passive TCP connection establishment,
 * data transfer and active close
 */
testcase test_tcp_listen_no_data_close() runs on tcp_ct
{
	test_tcp_listen(data_to_receive := '01'O);
}

/**
 * Test passive TCP connection establishment,
 * data transfer and active close
 */
testcase test_tcp_listen_data_close() runs on tcp_ct
{
	test_tcp_listen(data_to_receive := '01'O);
}

/**
 * Test passive TCP connection establishment,
 * data transfer and active close
 */
testcase test_tcp_listen_more_data_close() runs on tcp_ct
{
	test_tcp_listen(data_to_receive := '010203'O);
}

testcase test_tcp_listen_isn() runs on tcp_ct
{
	test_tcp_listen(isn := 333);
}

testcase test_tcp_connect_isn() runs on tcp_ct
{
	test_tcp_connect(isn := 333);
}

/**
 * Test that RST works in ESTABLISHED
 * TODO: Generalize to other states
 */
testcase test_tcp_listen_no_data_reset() runs on tcp_ct
{
	var udp_t udp;
	var tp_new_t tp;

	test_tcp_listen(close := false);

	tcp_config({{ "tp_trace", "1" }});

	P_TCP.send(ts_TCP(f := TH_RST, th_seq := seq));
	T.start;

	alt {
	[] P_UDP.receive(?) -> value udp {
		T.stop;
		tp := f_tp_new_dec(udp.data);
		if (tp.data[0].val == "CONN_DELETE") {
			setverdict(pass);
		}
	}
	[] T.timeout { setverdict(fail); }
	} /* end of alt */

	tp_send(TP_COMMAND, "CLOSE");
}

/**
 * Test that RST works in ESTABLISHED
 * TODO: Generalize to other states
 */
testcase test_tcp_connect_no_data_reset() runs on tcp_ct
{
	var udp_t udp;
	var tp_new_t tp;

	test_tcp_connect(close := false);

	tcp_config({{ "tp_trace", "1" }});

	P_TCP.send(ts_TCP(f := TH_RST, th_seq := seq));
	T.start;

	alt {
	[] P_UDP.receive(?) -> value udp {
		T.stop;
		tp := f_tp_new_dec(udp.data);
		if (tp.data[0].val == "CONN_DELETE") {
			setverdict(pass);
		}
	}
	[] T.timeout { setverdict(fail); }
	} /* end of alt */

	tp_send(TP_COMMAND, "CLOSE");
}

/**
 * Test that peer sends RST on missing data in ESTABLISHED
 */
testcase test_tcp_connect_data_gap() runs on tcp_ct
{
	var udp_t udp;
	var tp_new_t tp;
	var boolean reset_received := false;

	test_tcp_connect(close := false);

	tcp_config({{ "tp_trace", "1" }});

	P_TCP.send(ts_TCP(f := TH_PSH, th_seq := seq + 42));
	T.start;

	alt {
	[] P_TCP.receive(tr_TCP(f := TH_RST)) {
		T.stop;
		reset_received := true;
		T.start; repeat;
	}
	[reset_received] P_UDP.receive(?) -> value udp {
		T.stop;
		tp := f_tp_new_dec(udp.data);
		if (tp.data[0].val == "CONN_DELETE") {
			setverdict(pass);
		}
	}
	[] T.timeout { setverdict(fail); }
	} /* end of alt */

	tp_send(TP_COMMAND, "CLOSE");
}

testcase test_tcp_listen_data_reset() runs on tcp_ct
{
	test_tcp_listen(data_to_receive := '01'O);
}

testcase test_tcp_connect_data_reset() runs on tcp_ct
{
	test_tcp_connect(data_to_send := '01'O);
}

/**
 * Test TCP debug
 */
testcase test_tcp_debug() runs on tcp_ct
{
	tcp_init();

	TT.start;

	tp_send(TP_DEBUG_STOP);

	P_TCP.send(ts_TCP(f := TH_SYN, th_seq := post_inc(seq)));
	T.start;

	alt {
	[] P_TCP.receive(tr_TCP(?)) {
		T.stop;
		setverdict(fail);
	}
	[] T.timeout {
		tp_send(TP_DEBUG_STEP);
	}
	} /* end of alt */

	T.start;
	alt {
	[] P_TCP.receive(tr_TCP(?)) {
		T.stop;
		setverdict(pass);
	}
	[] T.timeout {
		setverdict(fail);
	}
	} /* end of alt */

	tp_send(TP_COMMAND, "CLOSE");
}

/**
 * Test peer's retransmission
 */
testcase test_tcp_retransmit() runs on tcp_ct
{
	tcp_init();

	fuzzer_on();

	P_FZ.send("incoming_drop");

	P_TCP.send(ts_TCP(f := TH_SYN, th_seq := post_inc(seq)));
	st(TCP_SYN_SENT);
	T.start(T_default * 3.0);
	T.timeout;
	P_FZ.send("incoming_pass");

	TT.start;
	T.start(T_default * 3.0);

	alt {
	[] P_TCP.receive(tr_TCP(f := TH_SYN or4b TH_ACK,
							th_ack := seq)) -> value tcp {
		T.stop;
		ack := tcp.th_seq;
		P_TCP.send(ts_TCP(f := TH_ACK, th_seq := seq, th_ack := pre_inc(ack)));
		setverdict(pass);
	}
	[] T.timeout {
		setverdict(fail);
	}
	}

	T.start(T_default);
	T.timeout;

	st(TCP_ESTABLISHED, fail_on := EV_NONE);

	tp_send(TP_COMMAND, "CLOSE");
}

template th_flags_t tr_TH_ACK	:='????1?'B;
template th_flags_t tr_TH_PSH	:='???1??'B;

function tcp_send(octetstring data := ''O) runs on tcp_ct
{
	/* This check might be too restrictive for out of sync tests */
	peer_assert_new(exp_seq := ack, exp_ack := seq);

	P_TCP.send(ts_TCP(f := TH_PSH, th_seq := post_inc(seq, lengthof(data)),
					  th_ack := ack, d := data));
	T.start;

	alt {
		[] P_TCP.receive(tr_TCP(f := TH_ACK, th_seq := ack, th_ack := seq)) {
			T.stop;
		}
		[] P_TCP.receive(?) { T.stop; setverdict(fail); }
		[] T.timeout { setverdict(fail); }
	} /* end of alt */

	peer_assert_new(exp_seq := ack, exp_ack := seq);
}

#define tcp_assert(_cond, args...) do {							\
	if ((_cond) == false) {										\
		log("Assertion failed: ", #_cond, " ", ## args);		\
		setverdict(fail);										\
		tp_send(TP_COMMAND, "CLOSE");							\
		stop;													\
	}															\
} while (false)

function peer_assert_new(tcp_state_t exp_state := TCP_NONE,
						 integer exp_seq := -1,
						 integer exp_ack := -1) runs on tcp_ct return tp_t
{
	var tp_t peer := tp_send(TP_INTROSPECT_REQUEST);

	if (exp_state != TCP_NONE) {
			tcp_assert(peer.state == tcp_state_to_string(exp_state),
				   "peer.state=", peer.state, " exp_state=", exp_state);
	}

	if (exp_seq >= 0) {
		tcp_assert(peer.seq == exp_seq,
			   "peer.seq=", peer.seq, ", exp_seq=", exp_seq);
	}

	if (exp_ack >= 0) {
		tcp_assert(peer.ack == exp_ack,
			   "peer.ack=", peer.ack, ", exp_ack=", exp_ack);
	}

	return peer;
}

/**
 * Test the reception of data with variable chunks, verify peer's seq and ack,
 * and check what the peer has received with the TP_COMMAND/RECV
 */
/* TODO: Enhance this one with out of order data */
testcase test_tcp_recv() runs on tcp_ct
{
	var tcp_test_data_t data := { '01'O, '0203'O, '040506'O };
	var octetstring data_total := ''O;

	test_tcp_connect(close := false);

	tcp_config({{ "tcp_echo", "0" }});

	for (var integer i := 0; i < sizeof(data); i := i + 1) {
		data_total := data_total & data[i];
		tcp_send(data[i]);
	}

	var tp_t peer := tp_send(TP_COMMAND, "RECV");

	if (str2oct(peer.data) == data_total) {
		setverdict(pass);
	}

	tp_send(TP_COMMAND, "CLOSE");
}

/**
 * Test peer's tcp_send() functionality
 */
testcase test_tcp_send() runs on tcp_ct
{
	test_tcp_connect(close := false);

	tcp_config({{ "tcp_echo", "0" }});

	tp_send(TP_COMMAND, "SEND", "01");

	T.start(T_default * 10.0);
	T.timeout;

	tp_send(TP_COMMAND, "CLOSE");
}

testcase test_eth() runs on tcp_ct
{
	tcp_init();
}

testcase test_tcp_undersized() runs on tcp_ct
{
	tcp_init();

	var ip_t ip := valueof(ts_IP(IPPROTO_TCP, ''O));

	P_IP.send(ip);

	setverdict(pass); /* TODO: check peer's state */
}

/**
 * Send an undersized data in ESTABLISHED
 */
/* TODO: Implement fuzzying of ip_len/th_off/data */
testcase test_tcp_undersized2() runs on tcp_ct
{
	test_tcp_connect(close := false);

	tcp_config({{ "tp_trace", "1" }, { "tp_conn_delete", "0" }});

	var octetstring data := ''O;

	T.start;
	T.timeout;

	P_TCP.send(ts_TCP(f := TH_PSH, th_seq := post_inc(seq, lengthof(data)),
					  d := data));

	peer_assert_new(exp_seq := ack, exp_ack := seq);

	setverdict(pass);
}

type record map_entry_t {
	charstring key,
	charstring val /* "value" is a reserved word in TTCN-3 */
} with {
	encode "JSON";
	variant(val) "JSON: name as value";
}

type record of map_entry_t map_t with {
	encode "JSON";
}

type record tp_new_t {
	charstring msg,
	map_t data
} with {
	encode "JSON";
}

template tp_new_t ts_TP_new(tp_msg_t msg, map_t data) := {
	msg := tp_msg_to_string(msg),
	data := data
}

external function f_tp_new_enc(in tp_new_t x) return octetstring
	with { extension "prototype(convert) encode(JSON) printing(pretty)"; }

external function f_tp_new_dec(in octetstring x) return tp_new_t
	with { extension "prototype(convert) decode(JSON)"; }

function tp_send_new(tp_msg_t msg, map_t data) runs on tcp_ct
{
	var tp_new_t tp := valueof(ts_TP_new(msg, data));

	P_UDP.send(ts_UDP(d := f_tp_new_enc(tp)));
}

external function f_enc_map(in map_t x) return octetstring
	with { extension "prototype(convert) encode(JSON)" }


function tcp_config(map_t cfg) runs on tcp_ct
{
	tp_send_new(TP_CONFIG_REQUEST, cfg);
}

testcase test_config() runs on tcp_ct
{
	var map_t cfg := { { "tcp_echo", "1" } };

	test_ct_init();

	tcp_config(cfg);
}

function tcp_init() runs on tcp_ct
{
	test_ct_init();

	tcp_config({ { "tcp_echo", "1" }, { "tp_tcp_conn_delete", "0" } });
}

#define assert(_cond, args...) do {							\
	if ((_cond) == false) {									\
		log("Assertion failed: ", #_cond, " ", ## args);	\
		setverdict(fail);									\
		stop;												\
	}														\
} while (false)

type record win_t {
	integer len,
	octetstring data
}

function win_init(inout win_t w)
{
	w.len := 0;
	w.data := ''O;
}

function win_push(inout win_t w, octetstring data)
{
	assert(lengthof(data) > 0, "data=", data);

	w.len := w.len + lengthof(data);
	w.data := w.data & data;
}

function win_pop(inout win_t w, integer len) return octetstring
{
	var octetstring data;

	assert(len <= w.len, "len=", len, ", w.len=", w.len);

	w.len := w.len - len;
	data := substr(w.data, 0, len);
	w.data := replace(w.data, 0, len, ''O);

	return data;
}

function win_peek(inout win_t w, integer len) return octetstring
{
	assert(len <= w.len, "len=", len, ", w.len=", w.len);

	return substr(w.data, 0, len);
}

type record of integer vec_t;

/**
 * Experimental test with the send window and data resend
 */
testcase test_tcp_window() runs on tcp_ct
{
	var win_t to_send, unack;
	var vec_t step := { 1, 2, 3, 4, 1, 2, 3, 4 };
	var integer i := 0;
	var octetstring data;

	win_init(to_send);
	win_init(unack);

	test_tcp_connect(close := false);

	tcp_config({{ "tcp_echo", "0" }});

	win_push(to_send, '0102030405060708081011121314151617181920'O);

	P_FZ.send("incoming_drop");

	while (to_send.len > 0) {

		data := win_pop(to_send, step[i]);

		P_TCP.send(ts_TCP(f := TH_PSH,
						  th_seq := (seq + unack.len),
						  d := data));

		win_push(unack, data);

		pre_inc(i);
	}

	/* TODO: Timing shouldn't be hardcoded, find a method for adaptive timing */
	T.start(T_default * 3.0); T.timeout;

	P_FZ.send("incoming_pass");

	i := 0;

label resend;
	while (unack.len > 0) {
		data := win_peek(unack, 1);

		P_TCP.send(ts_TCP(f := TH_PSH,
						  th_seq := seq + lengthof(data),
						  d := data));
		alt {
		[] P_TCP.receive(tr_TCP(f := TH_ACK, th_seq := ack)) -> value tcp {

			var integer acked := tcp.th_ack - seq;

			assert(acked > 0);
			assert(acked <= unack.len);

			log("acked=", acked);

			seq := seq + acked;
			win_pop(unack, acked);

			goto resend;
		}
		[] P_TCP.receive(?) -> value tcp {
			assert(false, "tcp=", tcp);
		}
		} /* end of alt */
	}

	var tp_t peer := peer_assert_new(); /* get peer's received data */

	log(peer.data);

	/* TODO: Implement a check here */

	setverdict(pass);

	tp_send(TP_COMMAND, "CLOSE");
}

/**
 * Test sequence wraps/overflows
 */
testcase test_tcp_seq_wrap() runs on tcp_ct
{
	test_tcp_connect(close := false, isn := hex2int('FFFFFFFE'H));
}

#undef assert
#define assert(_cond, args...) do {							\
	if ((_cond) == false) {									\
		log(__FILE__, ":", __LINE__, " ",					\
			"Assertion failed: ", #_cond, ", ", ## args);	\
		setverdict(fail);									\
		stop;												\
	}														\
} while (false)

/* Convert float value in seconds to integer in milliseconds */
#define TO_MSEC(_t) float2int((_t) * 1000.0)

/**
 * This a fork of the retransmission test for an experimentation
 * with what is the best approach here.
 * Currently it checks peer's retransmission in TCP_SYN_RECEIVED
 * TODO:
 *  - Check the retransmission in peer's TCP_SYN_SENT, TCP_ESTABLISHED,
 *    TCP_LAST_ACK
 *  - During peer's retransmission, input messages from the previous
 *    and next state
 *  - Reply before the peer's retransmission count is reached and
 *    verify that peer can recover
 */
testcase test_tcp_retransmit2() runs on tcp_ct
{
	tcp_init();

	var udp_t udp;
	var tp_t tp;

	var integer tcp_retries := 10;
	var float tcp_rto := T_default/4.0;
	var boolean peer_deleted := false;

	ack := -1;

	tcp_config({
			{ "tcp_rto", int2str(TO_MSEC(tcp_rto)) },
			{ "tcp_retries", int2str(tcp_retries) },
			{ "tp_trace", int2str(1) }
	});

	P_TCP.send(ts_TCP(f := TH_SYN, th_seq := post_inc(seq)));
	st(TCP_SYN_SENT);
	T.start;

	tcp_retries := tcp_retries + 1; /* account non retransmit */

	alt {
	[tcp_retries > 0] P_TCP.receive(tr_TCP(f := TH_SYN or4b TH_ACK,
							th_ack := seq)) -> value tcp {
		log("tcp_rto=", tcp_rto, " sec, actual=", T.read, " sec");
		T.stop;

		if (ack < 0) {
			ack := tcp.th_seq;
		}

		assert(tcp.th_seq == ack);

		if (tcp_retries == 0) {
			peer_assert(TCP_SYN_SENT);
		}

		tcp_retries := tcp_retries - 1;
		T.start;
		repeat;
	}
	[tcp_retries <= 0] P_TCP.receive(?) -> value tcp {
		log("Extra retransmit");
		setverdict(fail);
	}
	[tcp_retries == 0] P_UDP.receive(?) -> value udp {
		T.stop;
		tp := f_tp_dec(udp.data);
		peer_deleted := true;
		setverdict(pass);
	}
	[] T.timeout { setverdict(fail); }
	} /* end of alt */
	//label end;
	//tp_send(TP_COMMAND, "CLOSE");
}

#define setverdict(args...) do { } while (false)

#define execute(args...) do {								\
	if (execute(args) != pass) {							\
		pre_inc(tests_failed);								\
	}														\
	assert(tests_failed < max_tests_failed,					\
		   "Maximum number of failed tests reached");		\
} while (false)

control {

	var integer i := number_of_runs, tests_failed := 0;

	while (post_dec(i) > 0) {
		execute(test_tcp_undersized());
		execute(test_tcp_connect_no_data_close());
		execute(test_tcp_connect_no_data_reset());
		execute(test_tcp_connect_data_close());
		execute(test_tcp_connect_more_data_close());
		execute(test_tcp_connect_data_gap());
		execute(test_tcp_listen_no_data_close());
		execute(test_tcp_listen_no_data_reset());
		execute(test_tcp_listen_data_close());
		execute(test_tcp_listen_more_data_close());
		execute(test_tcp_retransmit());
		execute(test_tcp_recv());
		/* execute(test_tcp_window());	TODO: Fix the issue with the test
										global teardown timer and include */
		//execute(test_tcp_debug());
	}

} /* end of control */

} /* end of module */
