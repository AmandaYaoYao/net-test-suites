/* -*- tab-width: 4; -*- */
/*
 * Copyright Â© 2018, Intel Corporation.
 *
 * This program is free software; you can redistribute it and/or modify it
 * under the terms and conditions of the GNU Lesser General Public License,
 * version 2.1, as published by the Free Software Foundation.
 *
 * This program is distributed in the hope it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 * FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
 * more details.
 *
 */

/*
 * This is a sanity check for experimental TCP implementation for Zephyr RTOS:
 * https://github.com/ozhuraki/zephyr/blob/tcp2-dev/subsys/net/ip/tcp2.c
 *
 * It is also an exploratory playground for bringing improvements
 * to tcp_suite.ttcn (which aims to be abstract from the system under test).
 *
 */

module tcp2_check {

import from libtypes all;
import from libnetinet all;
import from libtest all;
import from libutils all;
import from port_data_types all;

modulepar {
	float T_default := 0.1;
	boolean fuzzer_enabled := false;
	boolean peer_assert_enabled := true;
}

type bitstring event_t length(3);

const event_t EV_NONE			:='000'B;
const event_t EV_KEEP			:='111'B;
const event_t EV_TIMEOUT		:='001'B;
const event_t EV_UNEXPECTED_MSG :='010'B;

/* Fuzzer component for incoming/outgoing drop, delay, reorder, corrupt */
/* Also used for transparent debug */
type component fuzzer_ct {
	port port_tcp P_TCP_IN;		/* from/to suite */
	port port_tcp P_TCP_OUT;	/* from/to system under test */
	port port_cf P_CF;			/* configuration port */
}

type component tcp_ct extends test_CT {
	var tcp_t tcp;
	var tcp_state_t s := TCP_NONE;
	var uint32_t seq := 0;
	var uint32_t ack := 0;
	var octetstring data := ''O;
	var octetstring data_unack := ''O; /* split to rcv/snd data */
	var integer data_step := 1;
	timer T := T_default;
	var event_t events := EV_NONE;
	var default d_as_timeout;
	var default d_as_unexpected_msg;
	var fuzzer_ct ct_fuzzer;
}

function fuzzer() runs on fuzzer_ct
{
	var charstring cmd;
	var tcp_t tcp;

	while (true) {
		alt {
		[] P_CF.receive(?) -> value cmd { log("got command: ", cmd); }
		[] P_TCP_IN.receive(?) -> value tcp {
			log("sut <- suite");
			P_TCP_OUT.send(tcp);
		}
		[] P_TCP_OUT.receive(?) -> value tcp {
			log("sut -> suite");
			P_TCP_IN.send(tcp);
		}
		}
	}
}

altstep as_timeout() runs on tcp_ct
{
	[] T.timeout { log("as_timeout()"); setverdict(fail); stop; }
}

altstep as_unexpected_msg() runs on tcp_ct
{
	[] P_TCP.receive(?) { log("as_unexpected_msg()"); setverdict(fail); stop; }
}

#define EVENT_IS(_event, _kind) mask_is_present(_event, _kind)
#define EVENT_IS_SET(_event, _kind) mask_is_present(_event, _kind)
#define EVENT_SET(_event, _kind) mask_set(_event, _kind)
#define EVENT_CLEAR(_event, _kind) mask_clear(_event, _kind)

function fail_on_events(event_t event) runs on tcp_ct
{
	if (event == EV_KEEP) {
		return;
	}

	if (EVENT_IS(event, EV_TIMEOUT) and not EVENT_IS_SET(events, EV_TIMEOUT)) {
		d_as_timeout := activate(as_timeout());
		EVENT_SET(events, EV_TIMEOUT);
	}

	if (not EVENT_IS_SET(event, EV_TIMEOUT) and
			EVENT_IS_SET(events, EV_TIMEOUT)) {
		deactivate(d_as_timeout);
		EVENT_CLEAR(events, EV_TIMEOUT);
	}

	if (EVENT_IS(event, EV_UNEXPECTED_MSG) and not
			EVENT_IS_SET(events, EV_UNEXPECTED_MSG)) {
		d_as_unexpected_msg := activate(as_unexpected_msg());
		EVENT_SET(events, EV_UNEXPECTED_MSG);
	}

	if (not EVENT_IS_SET(event, EV_UNEXPECTED_MSG) and
			EVENT_IS_SET(events, EV_UNEXPECTED_MSG)) {
		deactivate(d_as_unexpected_msg);
		EVENT_CLEAR(events, EV_UNEXPECTED_MSG);
	}
}

function st(tcp_state_t new, event_t fail_on := EV_KEEP) runs on tcp_ct
{
	log(tcp_state_to_string(s), " -> ", tcp_state_to_string(new));
	s := new;
	fail_on_events(fail_on);
}

type enumerated jtcp_msg_type_t {
	JTCP_TCP_INTROSPECT_REQUEST,
	JTCP_TCP_INTROSPECT_RESPONSE,
	JTCP_MEMORY_INTROSPECT_REQUEST,
	JTCP_MEMORY_INTROSPECT_RESPONSE,
	JTCP_PACKETS_INTROSPECT_REQUEST,
	JTCP_PACKETS_INTROSPECT_RESPONSE,
	JTCP_DEBUG_STOP,
	JTCP_DEBUG_STEP,
	JTCP_DEBUG_CONTINUE,
	JTCP_DEBUG_RESPONSE,
	JTCP_DEBUG_BREAKPOINT_ADD,
	JTCP_DEBUG_BREAKPOINT_DELETE,
	JTCP_TRACE_ADD,
	JTCP_TRACE_DELETE
} with {
	encode "TEXT";
}

external function jtcp_msg_type_to_string(in jtcp_msg_type_t x)
		return charstring
	with { extension "prototype(convert) encode(TEXT)"; }

type record tcp_json {
	charstring msg_type,
	charstring status,
	charstring state,
	integer seq,
	integer ack,
	charstring data
} with {
	encode "JSON";
	variant(msg_type)	"JSON: name as msg_type";
	variant(status)	"JSON: name as status";
	variant(state)	"JSON: name as state";
	variant(seq)	"JSON: name as seq";
	variant(ack)	"JSON: name as ack";
	variant(data)	"JSON: name as data";
}

external function f_json_enc(in tcp_json x) return octetstring
	with { extension "prototype(convert) encode(JSON)" }

external function f_json_dec(in octetstring x) return tcp_json
	with { extension "prototype(convert) decode(JSON)" }

function peer_assert(tcp_state_t state) runs on tcp_ct
{
	if (not peer_assert_enabled) { return; }

	/* TODO: check how to omit */
	var tcp_json req := { msg_type := "JTCP_TCP_INTROSPECT_REQUEST", status := "", state := "", seq := 0, ack := 0, data := "" };
	var udp_t udp;

	P_UDP.send(ts_UDP(d := f_json_enc(req)));
	T.start;

	alt {
		[] P_UDP.receive(?) -> value udp {
			T.stop;
			var tcp_json json := f_json_dec(udp.data);

			log(json);

			if (json.state != tcp_state_to_string(state)) {
				log("ASSERT: ", json.state, " != ", tcp_state_to_string(state));
				setverdict(fail);
			}
		}
		[] T.timeout {
			log("peer_assert() timeout");
			setverdict(fail);
		}
	}
}

function fuzzer_on(octetstring data_to_send := ''O) runs on tcp_ct
{
	ct_fuzzer := fuzzer_ct.create;

	disconnect(self:P_TCP, eth_ct:P_TCP);

	connect(self:P_TCP, ct_fuzzer:P_TCP_IN);
	connect(ct_fuzzer:P_TCP_OUT, eth_ct:P_TCP);

	ct_fuzzer.start(fuzzer());
}

/**
 * Test active TCP connection establishment,
 * data transfer (data echoing expected) and active close
 */
testcase test_tcp_connect(octetstring data_to_send := ''O) runs on tcp_ct
{
	test_ct_init();

	if (fuzzer_enabled) { fuzzer_on(); }

	data_push(data, data_to_send);

	P_TCP.send(ts_TCP(f := TH_SYN, th_seq := post_inc(seq)));
	st(TCP_SYN_SENT, fail_on := EV_TIMEOUT or4b EV_UNEXPECTED_MSG);
	T.start;

	alt {
	[s == TCP_SYN_SENT] P_TCP.receive(tr_TCP(f := TH_SYN or4b TH_ACK,
											 th_ack := seq)) -> value tcp {
		T.stop;
		peer_assert(TCP_SYN_SENT);
		st(TCP_SYN_RECEIVED);
		ack := tcp.th_seq;
		P_TCP.send(ts_TCP(f := TH_ACK, th_seq := seq, th_ack := pre_inc(ack)));
		st(TCP_ESTABLISHED, fail_on := EV_NONE);
		//peer_assert(TCP_ESTABLISHED); /* TODO: Handle when debug is ready */
		T.start; repeat;
	}
	[s == TCP_ESTABLISHED] P_TCP.receive(tr_TCP) -> value tcp {
		T.stop;
		peer_assert(TCP_ESTABLISHED);
		var tcp_t tcp2 := valueof(ts_TCP);
		if (th_is_set(tcp, TH_PSH)) {
			th_set(tcp2, TH_ACK);
			pre_inc(ack, lengthof(tcp.data));
		}
		if (th_is_set(tcp, TH_ACK)) {
			if (tcp.th_ack > seq - lengthof(data_unack)) {
				var integer diff := tcp.th_ack - (seq - lengthof(data_unack));
				data_pop(data_unack, diff);
			}
		}
		if (tcp2.th_flags != TH_NONE) {
			tcp2.th_seq := seq;
			tcp2.th_ack := ack;
			P_TCP.send(tcp2);
		}
		T.start; repeat;
	}
	[s == TCP_ESTABLISHED] T.timeout {
		peer_assert(TCP_ESTABLISHED);
		if (lengthof(data_unack) > 0) { goto wait_for_data; }
		if (lengthof(data_to_send) > 0) {
			data_unack := data_pop(data_to_send, data_step);
			P_TCP.send(ts_TCP(f := TH_PSH,
							  th_seq := post_inc(seq, lengthof(data_unack)),
							  th_ack := ack, d := data_unack));
			goto wait_for_data;
		}
		/* Everything is sent and aknowledged, do active close */
		P_TCP.send(ts_TCP(f := TH_FIN or4b TH_ACK, th_seq := post_inc(seq),
						  th_ack := ack));
		st(TCP_FIN_WAIT1, fail_on := EV_TIMEOUT or4b EV_UNEXPECTED_MSG);
		/* TODO: peer_assert() here is tricky, since peer in active close
		   sends ack and fin+ack immedeately */
label wait_for_data;
		T.start; repeat;
	}
	[s == TCP_FIN_WAIT1] P_TCP.receive(tr_TCP(f := TH_ACK)) {
		T.stop;
		/* TODO: peer_assert() here is tricky, since peer in active close
		   sends ack and fin+ack immedeately */
		st(TCP_FIN_WAIT2);
		T.start; repeat;
	}
	[s == TCP_FIN_WAIT2] P_TCP.receive(tr_TCP(f := TH_FIN or4b TH_ACK)) {
		T.stop;
		peer_assert(TCP_LAST_ACK);
		P_TCP.send(ts_TCP(f := TH_ACK, th_seq := seq, th_ack := pre_inc(ack)));
		st(TCP_TIME_WAIT, fail_on := EV_NONE);
		T.start; repeat; /* T * 2 */
	}
	[s == TCP_TIME_WAIT] T.timeout {
		st(TCP_CLOSED);
		peer_assert(TCP_CLOSED);
	}
	}

	if (s == TCP_CLOSED and
			lengthof(data_to_send) == 0 and
			lengthof(data_unack) == 0) {
		setverdict(pass);
	}
}

/**
 * Test passive TCP connection establishment
 */
testcase test_tcp_listen(octetstring data_to_send := ''O) runs on tcp_ct
{
	test_ct_init();

	data_push(data, data_to_send);
}

function jtcp_cmd(jtcp_msg_type_t msg_type) runs on tcp_ct
{
	var charstring cmd := jtcp_msg_type_to_string(msg_type);
	/* TODO: Template here? */
 	var tcp_json req := { msg_type := cmd, status := "",
						  state := "", seq := 0, ack := 0, data := "" };
	var udp_t udp;

	P_UDP.send(ts_UDP(d := f_json_enc(req)));

	/* alt { */
	/* 	[] P_UDP.receive(?) -> value udp { } */
	/* 	[] T.timeout { setverdict(fail); } */
	/* } */
}

/**
 * Test TCP debug
 */
testcase tc_tcp_debug() runs on tcp_ct
{
 	test_ct_init();

	jtcp_cmd(JTCP_DEBUG_STOP);

	P_TCP.send(ts_TCP(f := TH_SYN, th_seq := post_inc(seq)));

	T.start;
	T.timeout;

	jtcp_cmd(JTCP_DEBUG_STEP);
}

control {
	execute(test_tcp_connect(data_to_send := '01'O));
}

} /* end of module */
