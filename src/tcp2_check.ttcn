/* -*- tab-width: 4; -*- */
/*
 * Copyright Â© 2018, Intel Corporation.
 *
 * This program is free software; you can redistribute it and/or modify it
 * under the terms and conditions of the GNU Lesser General Public License,
 * version 2.1, as published by the Free Software Foundation.
 *
 * This program is distributed in the hope it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 * FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
 * more details.
 *
 */

/*
 * This is a sanity check for experimental TCP implementation for Zephyr RTOS:
 * https://github.com/ozhuraki/zephyr/blob/tcp2-dev/subsys/net/ip/tcp2.c
 */

module tcp2_check {

import from libtypes all;
import from libnetinet all;
import from libtest all;
import from libutils all;
import from port_data_types all;

modulepar {
	float T_default := 2.0;
}

type component tcp_ct extends test_CT {
	var tcp_t tcp;
	var tcp_state_t s := TCP_NONE;
	var integer msn := 0; /* my sequence number */
	var integer psn := 0; /* peer sequence number */
	timer T := T_default;
}

altstep as_timeout() runs on tcp_ct
{
	[] T.timeout { setverdict(fail); stop; }
}

altstep as_unexpected_msg() runs on tcp_ct
{
	[] P_TCP.receive(?) { setverdict(fail); stop; }
}

/**
 * Test active TCP connection establishment,
 * data transfer (data echoing expected) and active close
 */
testcase tc_tcp_conn(octetstring d := ''O) runs on tcp_ct
{
	test_ct_init();

	var octetstring data_to_send := d;
	var octetstring data_unack := ''O;
	var integer data_step := 1;

	var default d_as_timeout := activate(as_timeout());
	var default d_as_unexpected_msg := activate(as_unexpected_msg());

	T.start;

	alt {
	[s == TCP_NONE] T.timeout {
		P_TCP.send(ts_TCP(f := TH_SYN, sn := post_inc(msn)));
		s := TCP_SYN_SENT;
		T.start; repeat;
	}
	[s == TCP_SYN_SENT] P_TCP.receive(tr_TCP(f := TH_SYN or4b TH_ACK,
											 an := msn)) -> value tcp {
		log(s); T.stop;
		s := TCP_SYN_RECEIVED;
		psn := tcp.th_seq;
		P_TCP.send(ts_TCP(f := TH_ACK, sn := msn, an := pre_inc(psn)));
		T.start; repeat;
	}
	[s == TCP_SYN_RECEIVED] T.timeout {
		log(s);
		s := TCP_ESTABLISHED;
		deactivate(d_as_timeout); deactivate(d_as_unexpected_msg);
		T.start; repeat;
	}
	[s == TCP_ESTABLISHED] P_TCP.receive(tr_TCP) -> value tcp {
		log(s); T.stop;
		var tcp_t tcp2 := valueof(ts_TCP);
		if (th_is_set(tcp, TH_PSH)) {
			th_set(tcp2, TH_ACK);
			pre_inc(psn, lengthof(tcp.data));
		}
		if (th_is_set(tcp, TH_ACK)) {
			if (tcp.th_ack > msn - lengthof(data_unack)) {
				var integer diff := tcp.th_ack - (msn - lengthof(data_unack));
				data_pop(data_unack, diff);
			}
		}
		if (tcp2.th_flags != TH_NONE) {
			tcp2.th_seq := msn;
			tcp2.th_ack := psn;
			P_TCP.send(tcp2);
		}
		T.start; repeat;
	}
	[s == TCP_ESTABLISHED] T.timeout {
		log(s);
		if (lengthof(data_unack) > 0) { goto wait_for_data; }
		if (lengthof(data_to_send) > 0) {
			data_unack := data_pop(data_to_send, data_step);
			P_TCP.send(ts_TCP(f := TH_PSH,
							  sn := post_inc(msn, lengthof(data_unack)),
							  an := psn, d := data_unack));
			goto wait_for_data;
		}
		/* Everything is sent and aknowledged, do active close */
		s := TCP_FIN_WAIT1;
		P_TCP.send(ts_TCP(f := TH_FIN or4b TH_ACK, sn := post_inc(msn),
						  an := psn));
label wait_for_data;
		T.start; repeat;
	}
	[s == TCP_FIN_WAIT1] P_TCP.receive(tr_TCP(f := TH_ACK)) {
		log(s); T.stop;
		s := TCP_FIN_WAIT2;
		T.start; repeat;
	}
	[s == TCP_FIN_WAIT2] P_TCP.receive(tr_TCP(f := TH_FIN or4b TH_ACK)) {
		log(s); T.stop;
		P_TCP.send(ts_TCP(f := TH_ACK, sn := msn, an := pre_inc(psn)));
		s := TCP_TIME_WAIT;
		d_as_unexpected_msg := activate(as_unexpected_msg());
		T.start; repeat; /* T * 2 */
	}
	[s == TCP_TIME_WAIT] T.timeout {
		log(s);
		s := TCP_CLOSED;
		T.start; repeat;
	}
	[s == TCP_CLOSED] T.timeout {
		log(s);
		goto test_end;
	}

	}
label test_end;
	if (s == TCP_CLOSED and lengthof(data_to_send) == 0
			and lengthof(data_unack) == 0) {
		setverdict(pass);
	}
}

control {
	execute(tc_tcp_conn(d := '01'O));
}

}
